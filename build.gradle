buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = GROUP
version = VERSION_NAME

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 27
        testApplicationId "com.mixpanel.android.mpmetrics"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
        consumerProguardFiles 'proguard.txt'

        buildConfigField "String", "MIXPANEL_VERSION", "\"${version}\""

        lintOptions {
            abortOnError true
            warningsAsErrors false
            disable "UnusedResources"
            textOutput "stdout"
            textReport true
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
}

// Note- *all* dependencies are marked as optional in our final
// pom! If you need a transitive dependency in the library, you'll
// have to change the bit in uploadArchives that marks all dependencies as optional.
dependencies {
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    testCompile "org.mockito:mockito-core:3.0.0"
    androidTestCompile "org.mockito:mockito-android:3.0.0"
}

apply from: rootProject.file('maven.gradle')
